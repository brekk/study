import { Left, Right } from "Either"
import { read } from "File"
import IO from "IO"
import Parse from "Parse"
import String from "String"
import Test from "Test"
import Wish from "Wish"



TestError = Test.Error


assessParser :: (Show a, Eq a) => Parse.Parser a -> String -> a -> Wish Test.AssertionError {}
export assessParser = (parser, toParse, expected) => pipe(
  Parse.runParser(parser),
  where {
    Left(Parse.Error(Parse.Loc(a, b, c))) =>
      do {
        before = String.slice(0, a, toParse)
        after = String.slice(a, String.length(toParse), toParse)
        parseError = `Parsing error: (${show(a)} ${show(b)} ${show(c)})`
        detail = `
██████████████████████
➡︎${IO.green(before)}⬅︎
▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚▚
◣${IO.red(after)}◢
██████████████████████`
        IO.putLine(parseError ++ detail)
        return Wish.bad(TestError(parseError))
      }

    Right(res) =>
      Test.assertEquals(res, expected)
  },
)(toParse)

testParser :: (Show a, Eq a) => Parse.Parser a
  -> String
  -> String
  -> a
  -> Wish Test.TestResult Test.TestResult
export testParser = (parser, testName, toParse, expected) => Test.test(
  testName,
  () => assessParser(parser, toParse, expected),
)

testParseFile :: (Show a, Eq a) => String
  -> String
  -> Parse.Parser a
  -> a
  -> Wish Test.TestResult Test.TestResult
export testParseFile = (filePath, testName, parser, expected) => Test.test(
  testName,
  () => pipe(
    read,
    Wish.mapRej(() => TestError("File reading error")),
    chain((content) => assessParser(parser, content, expected)),
  )(filePath),
)
